ext {
    bintrayRepo = 'PaginatedTextView'
    bintrayName = 'PaginatedTextView'

    publishedGroupId = 'ru.mamykin.paginatedtextview'
    libraryName = 'PaginatedTextView'
    artifact = 'paginatedtextview'

    libraryDescription = 'Extended version of Android TextView which support pagination and clicks handling'

    siteUrl = 'https://github.com/mamykin-av/PaginatedTextView'
    gitUrl = 'https://github.com/mamykin-av/PaginatedTextView.git'

    libraryVersion = '0.1.0'

    developerId = 'mamykin-av'
    developerName = 'Andrey Mamykin'
    developerEmail = 'mamykin.av@bk.ru'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}